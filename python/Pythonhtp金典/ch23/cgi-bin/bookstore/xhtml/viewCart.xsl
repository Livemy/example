<?xml version = "1.0"?>

<!-- Fig. 23.17: viewCart.xsl                          -->
<!-- XSLT style sheet that transforms XML generated by -->
<!-- viewCart.py into XHTML.                           -->

<xsl:stylesheet version = "1.0"
   xmlns:xsl = "http://www.w3.org/1999/XSL/Transform">

   <xsl:output method = "xml" omit-xml-declaration = "no" 
      indent = "yes" doctype-system = "DTD/xhtml1-strict.dtd"
      doctype-public = "-//W3C//DTD XHTML 1.0 Strict//EN"/>

<xsl:template match = "cart">
   
   <html xmlns = "http://www.w3.org/1999/xhtml">

      <head>
         <title>Your Online Shopping Cart</title>
         <link rel = "stylesheet" href = "/bookstore/styles.css" 
            type = "text/css" />
      </head>

      <body>

         <p class = "bigFont">Shopping Cart</p>

         <xsl:choose>
            <xsl:when test = "@total = '0.00'">
               <p class = "bold">
                  Your shopping cart is currently empty.</p>
            </xsl:when>

            <xsl:otherwise>  <!-- total != 0.00 -->
               <table class = "cart">
                  <tr>
                     <th>Product</th>
                     <th>Quantity</th>
                     <th>Price</th>
                     <th>Total</th>    
                  </tr>

                  <xsl:apply-templates select = "orderProduct">  
                     
                     <!-- sort orderProducts by product/title -->
                     <xsl:sort select = "product/title" />
                  
                  </xsl:apply-templates>
                  
                  <tr>
                     <td colspan = "4" class = "bold right">
                        Total: <xsl:value-of select = "@total" />
                     </td>
                  </tr>
               </table>

            </xsl:otherwise>
         </xsl:choose>

         <p class = "bold green">
            <a href = "allBooks.py?ID=%s">Continue Shopping</a>
         </p>

         <form method = "post" action = "order.py?ID=%s">
            <p><input type = "submit" value = "Check Out" /></p>
         </form>

      </body>

   </html>  

</xsl:template>

<xsl:template match = "orderProduct">

   <tr>
      <td><xsl:value-of select = "product/title" />,
         <xsl:value-of select = "product/editionNumber" />e</td>
      <td><xsl:value-of select = "quantity" /></td>
      <td class = "right"><xsl:value-of select = 
         "product/price" /></td>
      <td class = "bold right"><xsl:value-of select = 
         "subtotal" /></td>
   </tr>

</xsl:template>

<!-- 
  (C) Copyright 2002 by Deitel & Associates, Inc. and         
  Prentice Hall. All Rights Reserved.                         
                                                              
  DISCLAIMER: The authors and publisher of this book have     
  used their best efforts in preparing the book. These        
  efforts include the development, research, and testing of   
  the theories and programs to determine their effectiveness. 
  The authors and publisher make no warranty of any kind,     
  expressed or implied, with regard to these programs or to   
  the documentation contained in these books. The authors     
  and publisher shall not be liable in any event for          
  incidental or consequential damages in connection with, or  
  arising out of, the furnishing, performance, or use of      
  these programs.                                             
 -->

</xsl:stylesheet>